*** Settings ***
Documentation   Test robotframework keywords for Smallworld Magik
...             Tests base connection to a smallworld swaf image with remote_cli via telnet
Resource         ../resources/robot_magik_base.txt
Suite Teardown   Close All Connections

*** Test Cases ***
Test Connect Image   [Documentation]   Test keyword 'Connect Magik Image'
    ${out}=   Connect Magik Image
    Should Match Regexp   ${out}   \\S+:\\d+:MagikSF>

Test Magik Command   [Documentation]  Test keyword 'Write Magik Command'
    ${out}=   Write Magik Command   1 + 1
    Should Be Equal   ${out}   ${none}
    ${out}=   Read Until Prompt
    Should Match Regexp   ${out}   \\s2 \\s\\S+:\\d+:MagikSF>

Test Magik Output   [Documentation]   Test keyword 'Read Magik Output'
    Write Bare   write("1 ernie", %newline, "2 bert", %newline, "3 bibo")\n$\n
    ${out}=   Read Magik Output
    Should Match Regexp   ${out}   ^1 ernie\\s2 bert\\s3 bibo$
    Write Bare   \n$\n
    ${out}=   Read Magik Output
    Write Bare   1.as_error()\n$\n
    Run Keyword And Expect Error   *traceback*   Read Magik Output

Test Execute Magik   [Documentation]  Test keyword 'Execute Magik Command'
    ${out}=   Execute Magik Command   write("1 erwin", %newline, "2 ulf", %newline, "3 hein")
    Should Be Equal As Strings   ${out}   3 hein
    ${out}=   Execute Magik Command   3 - 2
    Should Be Equal As Integers   ${out}   1

Test Define Variable   [Documentation]  Test keyword 'Define Magik Variable'
    Define Magik Variable   pybot  2
    ${out}=   Execute Magik Command   pybot + pybot
    Should Be Equal As Integers   ${out}   4
     

